plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}

// 'api' means the dependency will be published in the maven pom.
dependencies {
    api 'javax.inject:javax.inject:1'
    api 'org.seleniumhq.selenium:selenium-java:3.141.59'
    api 'com.google.inject:guice:4.2.3'
    api 'com.google.guava:guava:30.0-jre'
    api 'com.squareup.okio:okio:2.9.0'
    api 'com.github.docker-java:docker-java-core:3.2.7'
    api 'com.github.docker-java:docker-java-transport-httpclient5:3.2.7'
    
    implementation 'com.google.code.gson:gson:2.8.6'
    
    // Assertion libraries.
    api 'com.google.truth:truth:1.1'
    api 'org.assertj:assertj-core:3.18.1'
    api 'org.opentest4j:opentest4j:1.2.0'
    
    // Configuration library.
    api 'org.aeonbits.owner:owner-java8:1.0.10'
    api 'org.aeonbits.owner:owner-java8-extras:1.0.10'
    
    
    // Without this Mockito fails when running JUnit in Eclipse.
    // Errors look like the following.
    // org.mockito.exceptions.base.MockitoException:
    // Mockito cannot mock this class: interface org.openqa.selenium.WebElement.
    // Mockito can only mock non-private & non-final classes.
	implementation 'net.bytebuddy:byte-buddy:1.10.18'
    
    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.mockito:mockito-core:3.6.28'
}

group = 'net.ghue.jelenium'
version = '1.0.0-SNAPSHOT'
description = 'Jelenium'
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            // Include any other artifacts here, like javadocs
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lukelast/jelenium")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

}

